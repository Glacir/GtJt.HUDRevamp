
global function IonEnergy_Init
#if CLIENT
global function Ion_CreateEnergyBar
global function Ion_DestroyEnergyBar
global function UpdateIonEnergyBar
#endif

global const float ION_ENERGY_REGEN_INTERVAL = 0.1
global int ION_ENERGY_MAX = 1000

#if CLIENT
global function FlashEnergyNeeded_Bar

struct
{
	var ionEnergyBarRui = null
	bool useFix = false
} file
#endif

void function IonEnergy_Init()
{
	#if CLIENT
		AddLocalPlayerDidDamageCallback( Ion_CritHitIndicator )
		AddTitanCockpitManagedRUI( Ion_CreateEnergyBar, Ion_DestroyEnergyBar, Ion_ShouldCreateEnergyBar, RUI_DRAW_COCKPIT )
	#endif
}

#if CLIENT
var function Ion_CreateEnergyBar()
{
	Assert( file.ionEnergyBarRui == null )

	entity player = GetLocalViewPlayer()
	ION_ENERGY_MAX = player.GetSharedEnergyTotal()
	// cache, if user changes it after creating rui it would be a mess
	file.useFix = GetConVarBool("comp_hud_fix_ion_energy_bar")

	var rui
	if (ION_ENERGY_MAX > 1000)
	{
		if (GetConVarBool("comp_hud_use_short_energy_bar"))
		{
			rui = CreateTitanCockpitRui( $"ui/ion_energy_bar.rpak" )
		}
		else
		{
			rui = CreateTitanCockpitRui( $"ui/ion_energy_bar_Fd.rpak" )
		}
	}
	else
	{
		rui = CreateTitanCockpitRui( $"ui/ion_energy_bar.rpak" )
	}

	file.ionEnergyBarRui = rui

	RuiSetFloat( file.ionEnergyBarRui, "energyMax", float(ION_ENERGY_MAX) )
	if (!file.useFix)
	{
		RuiTrackFloat( file.ionEnergyBarRui, "energy", GetLocalViewPlayer(), RUI_TRACK_PLAYER_SHARED_ENERGY )
	}

	return file.ionEnergyBarRui
}

void function Ion_DestroyEnergyBar()
{
	TitanCockpitDestroyRui( file.ionEnergyBarRui )
	file.ionEnergyBarRui = null
}

bool function Ion_ShouldCreateEnergyBar()
{
	entity player = GetLocalViewPlayer()

	if ( !IsAlive( player ) )
		return false

	array<entity> mainWeapons = player.GetMainWeapons()
	if ( mainWeapons.len() == 0 )
		return false

	entity primaryWeapon = mainWeapons[0]
	if ( primaryWeapon.GetWeaponInfoFileKeyField( "npc_use_check_type" ) != "shared_energy" )
		return false

	return true
}

void function FlashEnergyNeeded_Bar( int neededEnergy )
{
	if ( file.ionEnergyBarRui == null )
		return

	RuiSetFloat( file.ionEnergyBarRui, "energyNeededRatio", mapping(float(neededEnergy)) / float(ION_ENERGY_MAX) )
	RuiSetGameTime( file.ionEnergyBarRui, "energyNeededFlashStartTime", Time() )
}

void function Ion_CritHitIndicator( entity attacker, entity victim, vector damagePos, int damageType )
{
	if ( file.ionEnergyBarRui == null )
		return

	if ( damageType & DF_CRITICAL )
		RuiSetGameTime( file.ionEnergyBarRui, "criticalHitFlashStartTime", Time() )
}

void function UpdateIonEnergyBar(entity player) {
	if ( file.ionEnergyBarRui == null || !file.useFix )
		return

	RuiSetFloat(file.ionEnergyBarRui, "energy", mapping(float(player.GetSharedEnergyCount())))
}

float function mapping(float original) {
	if (!file.useFix)
		return original
	return original * 0.88 + 0.06 * float(ION_ENERGY_MAX)
}
#endif
